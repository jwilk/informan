#!/usr/bin/env perl

# Copyright Â© 2022 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

no lib '.';  # CVE-2016-1238
use strict;
use warnings;
use English qw(-no_match_vars);

use Encode::Locale ();
use File::Which ();
use IPC::Run ();

sub embolden
{
    local ($_) = @_;
    s/(\X)/$1\b$1/g;
    $_;
}

my $text;
IPC::Run::run(['info', @ARGV], '>', \$text)
    or die 'info(1) failed';
$text = Encode::decode('locale', $text);
my $ul_chars = '*=-.';
my $ul_re = join('|', map { "[$_]+" } split(//, $ul_chars));
$text =~ s{^(\S.+)(?=\n(?:$ul_re)$)}{embolden($1)}mge;
$text = Encode::encode('locale', $text);
my $pager = $ENV{PAGER}
    // File::Which::which('pager')  # Debian <https://www.debian.org/doc/debian-policy/ch-customized-programs.html#editors-and-pagers>
    // 'more';  # POSIX <https://pubs.opengroup.org/onlinepubs/007904975/utilities/man.html#tag_04_85_08>
if ($pager eq 'cat') {
    $pager = undef;
}
if (not -t *STDOUT) {
    $pager = undef;
}
if (defined $pager) {
    IPC::Run::run(['sh', '-c', $pager], '<', \$text)
        or die "pager ($pager) failed";
} else {
    print $text;
}

# vim:ts=4 sts=4 sw=4 et
