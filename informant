#!/usr/bin/env perl

# Copyright Â© 2022 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

no lib '.';  # CVE-2016-1238
use strict;
use warnings;
use English qw(-no_match_vars);

use File::Which qw();
use IPC::Run qw();

my $text;
IPC::Run::run(['info', @ARGV], '>', \$text)
    or die 'info(1) failed';
my $pager = $ENV{PAGER}
    // File::Which::which('pager')  # Debian <https://www.debian.org/doc/debian-policy/ch-customized-programs.html#editors-and-pagers>
    // 'more';  # POSIX <https://pubs.opengroup.org/onlinepubs/007904975/utilities/man.html#tag_04_85_08>
if ($pager eq 'cat') {
    $pager = undef;
}
if (not -t *STDOUT) {
    $pager = undef;
}
if (defined $pager) {
    IPC::Run::run(['sh', '-c', $pager], '<', \$text)
        or die "pager ($pager) failed";
} else {
    print $text;
}

# vim:ts=4 sts=4 sw=4 et
