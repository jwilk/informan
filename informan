#!/usr/bin/env perl

# Copyright Â© 2022 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

no lib '.';  # CVE-2016-1238
use strict;
use warnings;

use English qw(-no_match_vars);
use Encode::Alias ();
use I18N::Langinfo (qw(langinfo CODESET));

use File::Which ();
use IPC::System::Simple ();

{
    my $encoding = langinfo(CODESET) // 'ASCII';
    Encode::Alias::define_alias('locale' => $encoding);
}

sub embolden
{
    local ($_) = @_;
    s/(\X)/$1\b$1/g;
    return $_;
}

my $text = IPC::System::Simple::capturex('info', @ARGV);
length $text > 0
    # This happens when info(1) can't find the page.
    # Huh? https://bugs.debian.org/1021827
    or die 'info(1) failed';
$text = Encode::decode('locale', $text);
my $ul_chars = '*=-.';
my $ul_re = join('|', map { "[$_]+" } split(//, $ul_chars));
$text =~ s{^(\S.+)(?=\n(?:$ul_re)$)}{embolden($1)}mge;
$text = Encode::encode('locale', $text);
my $pager = $ENV{PAGER}
    // File::Which::which('pager')  # Debian <https://www.debian.org/doc/debian-policy/ch-customized-programs.html#editors-and-pagers>
    // 'more';  # POSIX <https://pubs.opengroup.org/onlinepubs/007904975/utilities/man.html#tag_04_85_08>
if ($pager eq 'cat') {
    $pager = undef;
}
if (not -t *STDOUT) {
    $pager = undef;
}
if (@ARGV >= 1 and $ARGV[0] =~ m/\A(-h|--help|--version)\Z/) {
    $pager = undef;
}
if (defined $pager) {
    open(my $fh, '|-', 'sh', '-c', $pager)
        or die "pager: $ERRNO";
    print {$fh} $text
        or die "pager: $ERRNO";
    close($fh)
        or die 'pager: ' . ($ERRNO or 'failed');
} else {
    print $text
        or die $ERRNO;
}

# vim:ts=4 sts=4 sw=4 et
