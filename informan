#!/usr/bin/env perl

# Copyright Â© 2022 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

no lib '.';  # CVE-2016-1238

use v5.10;

use strict;
use warnings;

use charnames ':full';

use Encode::Alias ();
use English qw(-no_match_vars);
use File::Temp ();
use I18N::Langinfo (qw(langinfo CODESET));

use File::Which ();
use IPC::System::Simple ();

{
    my $encoding = langinfo(CODESET) // 'ASCII';
    Encode::Alias::define_alias('locale' => $encoding);
}

sub embolden
{
    local ($_) = @_;
    s/(\X)/$1\b$1/g;
    return $_;
}

sub fmt_xref
{
    (undef, my $space, local $_) = @_;
    state $arrow = do {
        my $s = "\N{RIGHTWARDS ARROW}";
        Encode::encode('locale', $s,
            sub { $s = '->'; }
        );
        $s =~ s/(.)/_\b$1/g;
        $s;
    };
    s/\c?//g;
    s/(?![.,]\z)((?=\S)\X)/_\b$1/g;
    $space =~ s/^ *//;
    return "$arrow ${space}$_";
}

my $tmpdir = File::Temp->newdir(TEMPLATE => 'informan.XXXXXX', TMPDIR => 1);
my $bin_false = File::Which::which('false')
    or die 'false(1) not found';
symlink($bin_false, "$tmpdir/man")
    or die "symlink(): $ERRNO";
my $PATH = $ENV{PATH} // '';
length $PATH > 0
    or die "empty $PATH is not supported";
$PATH = "$tmpdir:$PATH";
my @text = do {
    local $ENV{PATH} = $PATH;
    local $INPUT_RECORD_SEPARATOR = '';
    IPC::System::Simple::capturex('info', @ARGV);
};
scalar @text > 0
    # This can happen when info(1) can't find the page.
    # https://git.savannah.gnu.org/cgit/texinfo.git/commit/?id=15408a7b46ae1c0d
    or die 'info(1) failed';
my $node;
if ($text[0] =~ m/\AFile: (\S+)[.]info,  Node: (\S.*),  (?:Next|Up):/) {
    $node = "($1)$2";
}
my $ul_chars = '*=-.';
my $ul_re = join('|', map { "[$_]+" } split(//, $ul_chars));
my $qstring_re = qr/\c?[^\c?]+\c?/;
my $node_re = qr/[^,.:]+|$qstring_re/;
my $id_re = qr/(?:[(][^)]+[)])?$node_re/;
my $label_re = qr/[^:]+|$qstring_re/;
my $xref_re = qr/([*] *[Nn]ote)(\s++)(${id_re}::|$label_re:$id_re[.,])/;
for my $text (@text) {
    $text = Encode::decode('locale', $text);
    $text =~ s{^(\S.+)(?=\n(?:$ul_re)\n)}{embolden($1)}ge or
        $text =~ s{$xref_re}{fmt_xref($1, $2, $3)}ge;
    $text = Encode::encode('locale', $text);
}
my $pager = $ENV{PAGER}
    // File::Which::which('pager')  # Debian <https://www.debian.org/doc/debian-policy/ch-customized-programs.html#editors-and-pagers>
    // 'more';  # POSIX <https://pubs.opengroup.org/onlinepubs/007904975/utilities/man.html#tag_04_85_08>
if ($pager eq 'cat') {
    $pager = undef;
}
if (not -t *STDOUT) {
    $pager = undef;
}
if (@ARGV >= 1 and $ARGV[0] =~ m/\A(-h|--help|--version)\Z/) {
    $pager = undef;
}

sub get_less_options
{
    my ($title) = @_;
	my $prompt = 'Info';
    if (defined $title) {
        $title =~ s/([?:.%\\])/\\$1/g;
        # FIXME: less doesn't seem to offer any good way to escape $ characters.
        # --use-backslash may sound like what we want, but there's no apparent way
        # to undo it, so that it doesn't affect the original user's $LESS.
        # See also: https://bugs.debian.org/1021951
        my $repl_char = Encode::encode('locale', "\N{REPLACEMENT CHARACTER}");
        $title =~ s/\$/$repl_char/g;
        $prompt .= " $title";
    }
    # Shamelessly stolen from <https://git.savannah.nongnu.org/cgit/man-db.git/tree/src/man.c?h=2.9.4#n4173>:
    $prompt .= ', ?ltline %lt?L/%L.:byte %bB?s/%s..?e (END):?pB %pB\%..';
    $prompt .= ' (press h for help or q to quit)';
    my $LESS = $ENV{LESS} // '';
    return "-ix8mPm$prompt\$PM$prompt\$$LESS";
}

if (defined $pager) {
    local $ENV{LESS} = get_less_options($node);
    open(my $fh, '|-', 'sh', '-c', $pager)
        or die "pager: $ERRNO";
    for my $text (@text) {
        print {$fh} $text
            or die "pager: $ERRNO";
    }
    close($fh)
        or die 'pager: ' . ($ERRNO or 'failed');
} else {
    for my $text (@text) {
        print $text
            or die $ERRNO;
    }
}

# vim:ts=4 sts=4 sw=4 et
